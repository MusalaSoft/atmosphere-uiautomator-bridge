// This file is part of the ATMOSPHERE mobile testing framework.
// Copyright (C) 2016 MusalaSoft
//
// ATMOSPHERE is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// ATMOSPHERE is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with ATMOSPHERE.  If not, see <http://www.gnu.org/licenses/>.

apply plugin: 'maven-publish'
apply plugin: 'eu.appsatori.fatjar'

sourceCompatibility = 1.6
targetCompatibility = 1.6

import org.apache.tools.ant.taskdefs.condition.Os

version = '0.0.2'
jar {
    manifest {
        attributes 'Implementation-Title': 'Atmosphere UIAutomator Bridge',
                   'Implementation-Version': version
    }
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

project.ext {
    dexDir = new File("${buildDir}/dex")
    dexedJar = "${buildDir}/dex/${project.name}-${version}.jar"
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

def androidSdkHome = "${System.getenv('ANDROID_HOME')}"

dependencies {
    compile ('com.musala.atmosphere:framework-api19:0.+') {
        ext {
            fatJarExclude = true
        }
    }
    compile ('com.musala.atmosphere:uiautomator-api18:0.+') {
        ext {
            fatJarExclude = true
        }
    }
    compile 'de.mindpipe.android:android-logging-log4j:1.0.3'
    compile ('com.musala.atmosphere:atmosphere-agent-device-lib:0.+') {
        exclude module: 'framework-api19'
    }
    compile 'com.musala.atmosphere:openbeans-jxpath:0.+'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

publishing {
    publications {
        atmosphereUIAutomatorBridge(MavenPublication) {
            groupId 'com.musala.atmosphere'
            artifactId 'atmosphere-uiautomator-bridge'
            version version

            artifact source: project.dexedJar, extension: 'jar'
        }
    }

    repositories {
        mavenLocal()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

fatJar {
    doLast {
        tasks.dex.execute()
    }
}

// Credits here: http://wiliamsouza.github.io/#/2013/10/30/android-uiautomator-gradle-build-system
task dex(dependsOn: fatJar, type:Exec) {
    project.dexDir.mkdirs()
    workingDir '.'
    def dx = 'dx' + (Os.isFamily(Os.FAMILY_WINDOWS) ? '.bat' : '')
    commandLine androidSdkHome + '/' + androidSdkBuildToolsDir + '/' + dx, '--dex', '--no-strict', '--output=' + project.dexedJar, jar.archivePath
}

publishToMavenLocal.dependsOn fatJar

tasks.withType(JavaCompile) {
    options.bootClasspath = bootClasspath
}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
